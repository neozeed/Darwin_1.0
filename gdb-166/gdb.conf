# Configuration options to make gdb-4.17 appear more like the
# original NextStep gdb.

set width 0
set height 0

set history save on

handle SIGWINCH nostop noprint

set print pretty

define print-char
	if ($arg0 > 0xff)
		print "not a character"
		""
	else
		if ($arg0 == '\n')
			printf "\\n"
		else
			if ($arg0 == '\t')
				printf "\\t"
			else
				if ($arg0 == '\r')
					printf "\\r"
				else
					if ($arg0 == '\'')
						printf "\\'"
					else
						if (($arg0 < 0x20) || ($arg0 >= 0x7f))
							printf "\\%03o", $arg0
						else
							printf "%c", $arg0
						end
					end
				end
			end
		end
	end
end
document print-char
Print a single character in a readable fashion.
end

define print-ostype
	set $tmp0 = ($arg0)
	printf "'"
	set $tmp1 = (($tmp0 & 0xff000000) >> 24)
	print-char $tmp1
	set $tmp1 = (($tmp0 & 0x00ff0000) >> 16)
	print-char $tmp1
	set $tmp1 = (($tmp0 & 0x0000ff00) >> 8)
	print-char $tmp1
	set $tmp1 = (($tmp0 & 0x000000ff) >> 0)
	print-char $tmp1
	printf "'"
	printf "\n"
end
document print-ostype
Print a value as an OSType (four-byte character string).
end

define view-pb
        printf "Loading Objective-C bootstrap support.\n"
        load-plugin /System/Developer/PBBundles/Support/GDBPluginBootstrap.bundle/GDBPluginBootstrap
        printf "Loading View support bundle.\n"
        load-objc-bundle /System/Developer/PBBundles/Support/GDBViewPlugin.bundle
	set view-connection-name ProjectBuilder:ViewForGdb 
	view-start
end
document view-pb
Displays the current source file in ProjectBuilder.
end

define view-pbx
    printf "Loading Objective-C bootstrap support.\n" 
    load-plugin /System/Developer/PBBundles/Support/GDBPluginBootstrap.bundle/GDBPluginBootstrap 
    printf "Loading View support bundle.\n" 
    load-objc-bundle /System/Developer/PBBundles/Support/GDBViewPlugin.bundle 
    set view-connection-name PBX:ViewForGdb 
    view-start 
end 
document view-pbx
Displays the current line in PBX.
end
