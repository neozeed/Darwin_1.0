# Configuration for grep
#
# Alain Magloire <alainm@rcsm.ee.mcgill.ca>
#
dnl Process this file with autoconf to produce a configure script
AC_INIT(src/grep.c)
AC_DEFINE(GREP)
AC_PREREQ(2.13)

dnl Automake stuff.
AM_INIT_AUTOMAKE(grep, 2.3)
AM_CONFIG_HEADER(config.h:config.hin)

dnl Checks for programs.
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_RANLIB

# From Paul Eggert.
# If available, prefer support for large files unless the user specified
# one of the CPPFLAGS, LDFLAGS, or LIBS variables.
AC_LFS

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T
AC_C_CONST

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(string.h stdlib.h sys/param.h memory.h unistd.h libintl.h)
AC_CHECK_HEADERS(wctype.h wchar.h, USE_WCHAR=yes, USE_WCHAR=no)
AC_HEADER_DIRENT
AC_HEADER_STAT

dnl Checks for functions.
AC_FUNC_ALLOCA
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_MMAP
dnl getpagesize is checked for by AC_FUNC_MMAP.
AC_CHECK_FUNCS(strerror isascii setmode)
AC_REPLACE_FUNCS(memchr stpcpy)
dnl Solaris 2.5 doesn't have btowc()
if test "$USE_WCHAR" = "yes"; then
	AC_REPLACE_FUNCS(btowc)
dnl Solaris puts wctype/wXXX() in /usr/lib/libw.a
	AC_CHECK_LIB(w, wctype, [LIBS="$LIBS -lw"])
fi

dnl for VC++
if test x$ac_cv_prog_CC = xcl ; then
	AC_DEFINE(alloca, _alloca)
fi


dnl I18N feature
ALL_LINGUAS="de es el fr ko nl no pl ru sl sv"
AM_GNU_GETTEXT
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")

dnl DOS file name convention
dnl sets HAVE_DOS_FILE_NAMES
AC_DOSFILE

dnl check for the environ separator
dnl sets SEP
AM_SEP

dnl OS specifics
dnl sets {EXE,OBJ}EXT
AC_EXEEXT
AC_OBJEXT

dnl some folks ask for this, that's fine by me
dnl hope they know what they're doing ...
dnl if glibc2 regex is not included
jm_INCLUDED_REGEX(src/regex.c)

AC_OUTPUT(Makefile src/Makefile tests/Makefile po/Makefile.in intl/Makefile doc/Makefile m4/Makefile djgpp/Makefile vms/Makefile bootstrap/Makefile, [sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile; echo timestamp > stamp-h])
